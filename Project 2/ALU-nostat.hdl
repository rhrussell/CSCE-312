// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU-nostat.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not i.e. 1's complement
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not i.e. 1's complement
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not i.e. 1's complement

CHIP ALU-nostat {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // bitwise not (1's complement) the x input?
        zy, // zero the y input?
        ny, // bitwise not (1's complement) the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // bitwise not (1's complement) the out output?

    OUT 
        out[16]; // 16-bit output

    PARTS:     
        //For zx == 1 set x = 0   
        Mux16(a=x,b=false,sel=zx,out=x0);

        //For nx == 1 set x = !x
        Not16(in=x0,out=notx);
        Mux16(a=x0,b=notx,sel=nx,out=x1);

        //For zy == 1 set y = 0
        Mux16(a=y,b=false,sel=zy,out=y0);

        //For ny == 1 set y = !y
        Not16(in=y0,out=noty);
        Mux16(a=y0,b=noty,sel=ny,out=y1);

        //For f == 0 set out = x & y; f == 1 set out = x + y
        And16(a=x1,b=y1,out=xAndy); //f == 0
        Add16(a=x1,b=y1,out=xAddy); //f == 1
        Mux16(a=xAndy,b=xAddy,sel=f,out=out0);

        //For no == 1 set out = !out
        Not16(in=out0,out=notout);
        Mux16(a=out0,b=notout,sel=no,out=out);
}